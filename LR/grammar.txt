StmtList::=Stmt StmtList|epsilon
Stmt::=DeclareStmt|AssignOrCallOp|WhileOp|ForOp|IfOp|SwitchOp|IOp|OOp|lbrace StmtList rbrace|kwreturn E semicolon|semicolon
DeclareStmt::=Type id DeclareStmt'
DeclareStmt'::=lpar ParamList rpar lbrace StmtList rbrace|opassign num DeclVarList' semicolon|DeclVarList' semicolon
Type::=kwchar|kwint
DeclVarList'::=comma id InitVar DeclVarList'|epsilon
InitVar::=opassign num|opassign char|epsilon
ParamList::=Type id ParamList'|epsilon
ParamList'::=comma Type id ParamList'|epsilon
AssignOrCallOp::=AssignOrCall semicolon
AssignOrCall::=id AssignOrCall'
AssignOrCall'::=opassign E|lpar ArgList rpar
WhileOp::=kwwhile lpar E rpar Stmt
ForOp::=kwfor lpar ForInit semicolon ForExp semicolon ForLoop rpar Stmt
ForInit::= AssignOrCall|epsilon
ForExp::= E|epsilon
ForLoop::=AssignOrCall|opinc id|epsilon
IfOp::=kwif lpar E rpar Stmt ElsePart
ElsePart::=kwelse Stmt|epsilon
SwitchOp::=kwswitch lpar E rpar lbrace Cases rbrace
Cases::=ACase Cases'
Cases'::=ACase Cases'|epsilon
ACase::=kwcase num colon StmtList|default colon StmtList
IOp::=kwin id semicolon
OOp::=kwout OOp' semicolon
OOp'::=E|str
E::=E7
E7::=E6 E7'
E7'::=opor E6 E7'|epsilon
E6::=E5 E6'
E6'::=opand E5 E6'|epsilon
E5::=E4 E5'
E5'::=opeq E4|opne E4|opgt E4|oplt E4|ople E4|epsilon
E4::=E3 E4'
E4'::=opplus E3 E4'|opminus E3 E4'|epsilon
E3::=E2 E3'
E3'::=opmul E2 E3'|epsilon
E2::=opnot E1|E1
E1::=lpar E rpar|num|kwchar|opinc id|id E1'
E1'::=opinc|lpar ArgList rpar|epsilon
ArgList::=E ArgList'|epsilon
ArgList'::=comma E ArgList'|epsilon
